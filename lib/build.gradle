plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"
}

kotlin {
    explicitApiWarning()
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        lib(MavenPublication) {
            artifactId = "knio2"
            from components.java
            pom {
                packaging = 'jar'
                withXml {
                    def root = asNode()
                    new XmlParser().parse(buildFile.toPath().parent.resolve('pom-extra.xml').toFile()).each {
                        root.append(it)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "TheCentral"

            def final baseUrl = 'https://oss.sonatype.org'
            def final releasesUrl = "$baseUrl/service/local/staging/deploy/maven2"
            def final snapshotsUrl = "$baseUrl/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl

            credentials {
                username = getProperty("io.github.agcom.ossrh.username")
                password = getProperty("io.github.agcom.ossrh.password")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.lib
}